dnl Process this file with autoconf to produce a configure script.

dnl This is elrond's optional library check
AC_DEFUN(FP_CHECK_OPT_LIB,
         [AC_CHECK_FUNC([$2], :,
                        AC_CHECK_LIB($@)dnl
                       )dnl
         ])dnl

AC_INIT(librasta/rasta.h)

#
# Versioning ideas, copied from GTK+
#
# Making releases:
#   MICRO_VERSION += 1;
#   INTERFACE_AGE += 1;
#   BINARY_AGE += 1;
# If any functions have been added, set INTERFACE_AGE to 0.
# If backwards compatibility has been broken, set BINARY_AGE and
# INTEFACE_AGE to 0.
#
MAJOR_VERSION=0
MINOR_VERSION=1
MICRO_VERSION=4
INTERFACE_AGE=3
BINARY_AGE=3
VERSION="${MAJOR_VERSION}.${MINOR_VERSION}.${MICRO_VERSION}"
AC_SUBST(MAJOR_VERSION)
AC_SUBST(MINOR_VERSION)
AC_SUBST(MICRO_VERSION)
AC_SUBST(INTERFACE_AGE)
AC_SUBST(BINARY_AGE)
AC_SUBST(VERSION)

# libtool versioning
LT_RELEASE=$MAJOR_VERSION.$MINOR_VERSION
LT_CURRENT=`expr $MICRO_VERSION - $INTERFACE_AGE`
LT_REVISION=$INTERFACE_AGE
LT_AGE=`expr $BINARY_AGE - $INTERFACE_AGE`
LT_CURRENT_MINUS_AGE=`expr $LT_CURRENT - $LT_AGE`
AC_SUBST(LT_RELEASE)
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)
AC_SUBST(LT_CURRENT_MINUS_AGE)


AM_INIT_AUTOMAKE(rasta, $VERSION)
AM_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_PROG_CC
AM_C_PROTOTYPES
AM_PROG_CC_STDC
AC_C_CONST
AC_PROG_INSTALL

AM_MAINTAINER_MODE

AM_PROG_LIBTOOL

AC_ARG_ENABLE(ansi, [  --enable-ansi           turn on strict ansi [default=no]],
		    , enable_ansi=no)

if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[[\ \	]]-Wall[[\ \	]]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac

  if test "x$enable_ansi" = "xyes"; then
    case " $CFLAGS " in
    *[[\ \	]]-ansi[[\ \	]]*) ;;
    *) CFLAGS="$CFLAGS -ansi" ;;
    esac

    case " $CFLAGS " in
    *[[\ \	]]-pedantic[[\ \	]]*) ;;
    *) CFLAGS="$CFLAGS -pedantic" ;;
    esac
  fi
fi
  
AC_ARG_ENABLE(deprecated-fields, [  --enable-deprecated-fields turn on deprecated special-case field types [default=no]],, enable_deprecated=no)

if test "x$enable_deprecated" = "xyes"; then
    AC_DEFINE(ENABLE_DEPRECATED)
fi

CPPFLAGS="${CPPFLAGS} -DG_DISABLE_DEPRECATED -DGDK_DISABLE_DEPRECATED -DGTK_DISABLE_DEPRECATED"

dnl Checks for header files.
AC_HEADER_STDC

AC_CHECK_HEADERS(fcntl.h sys/ioctl.h unistd.h)

AC_PATH_PROG(LATEX2HTML, latex2html, /bin/false)

# Check for crypt
CRYPT_LIBS=
AC_CHECK_FUNCS(crypt, , AC_CHECK_LIB(crypt, crypt, CRYPT_LIBS="-lcrypt"))
AC_SUBST(CRYPT_LIBS)

# Check for readline
ldflags_orig=$LDFLAGS
LDFLAGS="$LDFLAGS -lcurses"
AC_CHECK_LIB(readline, readline, [
    AC_CHECK_HEADER(readline/readline.h, [
        AC_DEFINE(HAVE_READLINE)
        READLINE_LIBS="$LIBS -lreadline -lcurses"], [
        READLINE_LIBS=""])])
AC_SUBST(READLINE_LIBS)
LDFLAGS=$ldflags_orig

AC_CHECK_FUNC(strerror,
    ,
    AC_CHECK_LIB(44bsd, strerror, LIBS="-l44bsd $LIBS")
)

# Check for liblockfile
CGIRASTA=
ldflags_orig=$LDFLAGS
LDFLAGS="$LDFLAGS -llockfile"
AC_CHECK_LIB(lockfile, lockfile_check, [
    AC_CHECK_HEADER(lockfile.h, [
        AC_DEFINE(HAVE_LOCKFILE)
        CGIRASTA=cgirasta])])
AC_SUBST(CGIRASTA)
LDFLAGS=$ldflags_orig

PKG_CHECK_MODULES(GLIB, glib-2.0 >= 1.3.11 gmodule-2.0,,
    AC_MSG_ERROR([
*** GLIB 1.3.11 or better is required.  The latest version of GLIB
*** is always available from ftp://ftp.gtk.org/.]))

PKG_CHECK_MODULES(GTK, gtk+-2.0 >= 1.3.11,,
    AC_MSG_ERROR([
*** GTK+ 1.3.11 or better is required.  The latest version of GTK+
*** is always available from ftp://ftp.gtk.org/.]))

PKG_CHECK_MODULES(XML, libxml-2.0 > 2.3.0,,
            AC_MSG_ERROR([
*** LIBXML 2.3.0 or better is required.  The latest version of LIBXML
*** is always available from ftp://ftp.gnome.org/.]))

RASTA_DIR="${exec_prefix}"
test "x$RASTA_DIR" = xNONE && RASTA_DIR="${prefix}"
test "x$RASTA_DIR" = xNONE && RASTA_DIR="${ac_default_prefix}"
RASTA_DIR="${RASTA_DIR}/lib/rasta/rasta-${MAJOR_VERSION}"
AC_SUBST(RASTA_DIR)
AC_DEFINE_UNQUOTED(_RASTA_DIR, "$RASTA_DIR")

RASTA_NAMESPACE="http://www.jlbec.org/rasta/rasta-${MAJOR_VERSION}"
AC_SUBST(RASTA_NAMESPACE)
AC_DEFINE_UNQUOTED(RASTA_NAMESPACE, "$RASTA_NAMESPACE")

eval DTD_LOCATION="${datadir}/rasta/rasta-${MAJOR_VERSION}"
if test "x$prefix" = xNONE; then
    DTD_LOCATION="${ac_default_prefix}`echo "$DTD_LOCATION" | sed 's/^NONE//'`"
fi
AC_SUBST(DTD_LOCATION)
AC_DEFINE_UNQUOTED(_RASTA_DATA_DIR, "$DTD_LOCATION")

AC_OUTPUT([
Makefile
system.rasta
rasta-0.1.pc
librasta/Makefile
clrasta/Makefile
gtkrasta/Makefile
cgirasta/Makefile
tools/Makefile
documentation/Makefile
documentation/man/Makefile
documentation/man/man1/Makefile
documentation/man/man1/rastaadd.1
documentation/man/man1/rastadel.1
documentation/man/man1/gtkrasta.1
documentation/man/man1/clrasta.1
documentation/man/man1/cgirasta.1
documentation/man/man1/rastaedit.1
tests/Makefile
tests/test.rasta.tmpl
tests/testadd.rasta
tests/testdel.rasta
tests/teststate.rasta
examples/Makefile
examples/debian-package.rasta
examples/redhat-package.rasta
examples/network.rasta
redhat/rasta.spec
])
